buildscript {
    ext {
        kotlinVersion = '1.3.61'
        springBootVersion = '2.0.2.RELEASE'
        jUnitPlatformVersion = "1.0.0"
    }

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }

    dependencies {
        classpath "org.junit.platform:junit-platform-gradle-plugin:${jUnitPlatformVersion}"
    }
}

plugins {
    id "java"
    id "idea"
    id "application"
    id "org.springframework.boot" version "${springBootVersion}"
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.61"
    id "org.jetbrains.kotlin.jvm" version "1.3.61"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

project(":app:starter") {
    apply plugin: 'kotlin-spring'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'application'
}

configure([
        project(':app:core'),
        project(':app:shared'),
        project(':app:dto'),
        project(":app:starter"),
]) {

    apply plugin: 'idea'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        dependencies {
            dependency "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
            dependency "io.jsonwebtoken:jjwt:0.9.1"

            // FIXME Remove this dependency
            dependency "joda-time:joda-time:2.9.9"

            dependency "org.springframework.cloud:spring-cloud-starter-oauth2:2.0.0.RELEASE"
            dependency "org.springframework.security:spring-security-jwt:1.0.9.RELEASE"
            dependency "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.4.1"
            dependency "org.passay:passay:1.3.1"
            dependency "net.sourceforge.nekothml:nekothml:1.9.22"

            dependency "org.webjars:jquery:3.2.0"
            dependency "org.webjars:bootstrap:4.1.3"
            dependency "org.webjars:font-awesome:5.4.1"

            dependency "com.deliveredtechnologies:rulebook-core:0.8"
            dependency "com.guigarage:flatter:0.7"
            dependency "com.lowagie:itext:2.1.7"
            dependency "org.olap4j:olap4j:1.2.0"
            dependency "net.sf.jasperreports:jasperreports-functions:6.2.2"
            dependency("net.sf.jasperreports:jasperreports:6.4.3") {
                exclude "com.lowagie:itext"
                exclude "org.olap4j:olap4j"
            }

            dependency "net.lingala.zip4j:zip4j:1.3.2"
            dependency "net.harawata:appdirs:1.0.1"

            // Testing libraries
            dependency "org.mockito:mockito-core:3.2.4"
            dependency("com.nhaarman:mockito-kotlin:1.6.0") {
                exclude "org.mockito:mokito-core"
            }
            dependency "org.jetbrains.spek:spek-api:1.1.5"
            dependency "org.jetbrains.spek:spek-junit-platform-engine:1.1.5"
            dependency "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
        }
    }

    repositories {
        maven { url "http://dl.bintray.com/jetbrains/spek" }
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

        // common testing libraries
        testImplementation "org.mockito:mockito-core"
        testImplementation "com.nhaarman:mockito-kotlin"
        testImplementation "org.jetbrains.spek:spek-api"
        testImplementation "org.jetbrains.spek:spek-junit-platform-engine"
        testImplementation "org.jetbrains.kotlin:kotlin-test"
    }

    junitPlatform {
        filters {
            engines {
                include 'spek'
            }
        }
    }
}

configure([project(':app:core'), project(':app:shared')]) {
    apply plugin: 'kotlin-spring'
    apply plugin: 'org.springframework.boot'

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    dependencies {
        implementation "org.springframework.boot:spring-boot-starter-web"
    }
}
