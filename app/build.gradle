buildscript {
    ext {
        kotlinVersion = '1.2.60'
        springBootVersion = '2.0.2.RELEASE'
        jUnitPlatformVersion = "1.0.0"
    }

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }

    dependencies {
        classpath "org.junit.platform:junit-platform-gradle-plugin:${jUnitPlatformVersion}"
    }
}

plugins {
    id "java"
    id "idea"
    id "application"
    id "org.springframework.boot" version "${springBootVersion}"
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.61"
    id "org.jetbrains.kotlin.jvm" version "1.3.61"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

project(":app:starter") {
    apply plugin: 'kotlin-spring'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'application'
}


configure([
        project(':app:core'),
        project(':app:shared'),
        project(':app:dto'),
        project(":app:starter"),
        project(':app:desktop')
]) {

    apply plugin: 'idea'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.junit.platform.gradle.plugin'

    repositories {
        maven { url "http://dl.bintray.com/jetbrains/spek" }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

        // common testing libraries
        testImplementation 'org.mockito:mockito-core:2.9.0'
        // exclude mockito, because we need a different version
        testImplementation('com.nhaarman:mockito-kotlin:1.5.0') {
            exclude group: 'org.mockito'
        }
        testImplementation 'org.jetbrains.spek:spek-api:1.1.5'
        testImplementation 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'
        testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test', version: '1.2.50'
    }

    junitPlatform {
        filters {
            engines {
                include 'spek'
            }
        }
    }
}

configure([project(':app:core'), project(':app:shared')]) {
    apply plugin: 'kotlin-spring'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    dependencies {
        implementation('org.springframework.boot:spring-boot-starter-web')
    }
}

project(':app:shared') {
    dependencies {
        implementation project(':app:core')
        implementation project(':app:dto')
    }
}

project(':app:desktop') {
    dependencies {
        implementation project(':app:starter')
    }
}
