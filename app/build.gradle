buildscript {
    ext {
        kotlinVersion = '1.3.61'
        springBootVersion = '2.0.2.RELEASE'
        jUnitPlatformVersion = "1.0.0"
    }

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }

    dependencies {
        classpath "org.junit.platform:junit-platform-gradle-plugin:${jUnitPlatformVersion}"
    }
}

plugins {
    id "idea"
    id "org.springframework.boot" version "${springBootVersion}" apply false
    id "io.spring.dependency-management" version "1.0.9.RELEASE" apply false
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.61" apply false
    id "org.jetbrains.kotlin.jvm" version "1.3.61" apply false
    id "org.jlleitschuh.gradle.ktlint" version "9.2.1" apply false
}

project(":app:starter") {
    apply plugin: 'java'
    apply plugin: 'application'
    apply plugin: 'kotlin-spring'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'application'
}

configure([
        project(':app:core'),
        project(':app:shared'),
        project(':app:dto'),
        project(":app:starter"),
]) {

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jlleitschuh.gradle.ktlint'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencyManagement {
        dependencies {
            dependency "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
            dependency "io.jsonwebtoken:jjwt:0.9.1"

            // FIXME Remove this dependency
            dependency "joda-time:joda-time:2.9.9"

            dependency "org.springframework.cloud:spring-cloud-starter-oauth2:2.0.0.RELEASE"
            dependency "org.springframework.security:spring-security-jwt:1.0.9.RELEASE"
            dependency "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.4.1"
            dependency "org.passay:passay:1.3.1"
            dependency "net.sourceforge.nekothml:nekothml:1.9.22"

            dependency "org.webjars:jquery:3.2.0"
            dependency "org.webjars:bootstrap:4.1.3"
            dependency "org.webjars:font-awesome:5.4.1"

            dependency "com.deliveredtechnologies:rulebook-core:0.8"
            dependency "com.guigarage:flatter:0.7"
            dependency "com.lowagie:itext:2.1.7"
            dependency "org.olap4j:olap4j:1.2.0"
            dependency "net.sf.jasperreports:jasperreports-functions:6.2.2"
            dependency("net.sf.jasperreports:jasperreports:6.4.3") {
                exclude "com.lowagie:itext"
                exclude "org.olap4j:olap4j"
            }

            dependency "net.lingala.zip4j:zip4j:1.3.2"
            dependency "net.harawata:appdirs:1.0.1"

            // Testing libraries
            dependency "com.nhaarman:mockito-kotlin:1.6.0"
            dependency "org.spekframework.spek2:spek-dsl-jvm:2.0.9"
            dependency "org.spekframework.spek2:spek-runner-junit5:2.0.9"
            dependency "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
            dependency "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
        }
    }

    repositories {
        maven { url "https://dl.bintray.com/jetbrains/spek" }
        maven { url "https://jaspersoft.jfrog.io/jaspersoft/jr-ce-releases" }
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev/" }
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

        // common testing libraries
        testImplementation "com.nhaarman:mockito-kotlin"
        testImplementation "org.spekframework.spek2:spek-dsl-jvm"
        testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5"
        testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect"
        testImplementation "org.jetbrains.kotlin:kotlin-test"

    }

    test {
        useJUnitPlatform {
            includeEngines 'spek2'
        }
    }
}

configure([project(':app:core'), project(':app:shared')]) {
    apply plugin: 'kotlin-spring'
    apply plugin: 'org.springframework.boot'

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    dependencies {
        implementation "org.springframework.boot:spring-boot-starter-web"
    }
}
