buildscript {
	ext {
		kotlinVersion = '1.2.50'
		springBootVersion = '2.0.2.RELEASE'
		sporttagPSAVersion = '2.0.0'

        /*
         * Returns the main class depending on the mode command line parameter.
         *
         * -Pmode=gui uses the JavaFX Application
         * -Pmode=tui uses the Spring Application
         *
         * If the mode is not defined at all, 'tui' mode will be used.
         */
        getMainClass = {

            def mode = project.hasProperty('mode') ? project.property('mode') : 'tui'

            if (mode == 'gui') {
                return 'ch.schulealtendorf.sporttagpsa.FXApplicationKt'
            } else if (mode == 'tui') {
                return 'ch.schulealtendorf.sporttagpsa.SporttagPsaApplicationKt'
            }
            
            throw new IllegalArgumentException('Parameter \'mode\' is invalid: value=' + mode)
        }
	}
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		mavenCentral()
        
	}
	dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"

        // Kotlin all open to make specific classes open when compiling e.g. spring's @Configuration classes
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"

        // JUnit Platform TestEngine for Spek
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.0"

        // JavaFX Plugin to build native bundles
        classpath group: 'de.dynamicfiles.projects.gradle.plugins', name: 'javafx-gradle-plugin', version: '8.8.2'
	}
}

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'application'
apply plugin: 'javafx-gradle-plugin'

mainClassName = getMainClass()

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

version = "${sporttagPSAVersion}"

project(":rules") {

    apply plugin: 'idea'
    apply plugin: 'kotlin'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    }
}

project(":filesystem") {

    apply plugin: 'idea'
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }
    
	dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
		compile group: 'org.springframework.boot', name: 'spring-boot', version: "$springBootVersion"
	}
}

repositories {
	maven { url 'https://jitpack.io' }
	maven {
		url "https://jaspersoft.artifactoryonline.com/jaspersoft/jr-ce-releases/"
	}
    maven { url "http://dl.bintray.com/jetbrains/spek" }
    maven { url "https://dl.bintray.com/kotlin/kotlin-dev/"}
    mavenCentral()
}

dependencies {

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile("org.springframework.boot:spring-boot-starter-security")
	compile("org.springframework.boot:spring-boot-devtools")

    compile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'
    compile "joda-time:joda-time:2.9.9"
	compile "com.deliveredtechnologies:rulebook-core:0.8"
	compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.9.4.1'
	
	compile project(":rules")
    compile project(":filesystem")

	compile 'com.github.BilledTrain380:Alberta:1.0.0'
	
    // database dependencies
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.flywaydb:flyway-core')
	runtime('com.h2database:h2')

    // https://mvnrepository.com/artifact/org.springframework.cloud/spring-cloud-starter-oauth2
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-oauth2', version: '2.0.0.RELEASE'

    // https://mvnrepository.com/artifact/org.springframework.security/spring-security-jwt
    compile group: 'org.springframework.security', name: 'spring-security-jwt', version: '1.0.9.RELEASE'

    // https://mvnrepository.com/artifact/org.passay/passay
    compile group: 'org.passay', name: 'passay', version: '1.3.1'

    // to enable thymeleaf legacy html5 mode
	compile group: 'net.sourceforge.nekohtml', name: 'nekohtml', version: '1.9.22'

	compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.+"
	
	// webjars
	compile 'org.webjars:jquery:3.2.0'
	compile 'org.webjars:bootstrap:4.1.3'
	compile 'org.webjars:font-awesome:5.4.1'

    // exclude mockito, because we need a different version
    testCompile('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.mockito'
    }

    // common testing libraries
    testCompile 'org.mockito:mockito-core:2.9.0'
    // exclude mockito, because we need a different version
    testCompile ('com.nhaarman:mockito-kotlin:1.5.0') {
        exclude group: 'org.mockito'
    }
    testCompile 'org.jetbrains.spek:spek-api:1.1.5'
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'
    testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test', version: '1.2.50'
}

task processWebapp(type: Copy) {
    dependsOn ':psa-kitten:build'
    mustRunAfter "processResources"
    from 'psa-kitten/dist'
    into 'build/resources/main/public/app'
}

bootJar.dependsOn("processWebapp")

jfx {
    mainClass = "org.springframework.boot.loader.JarLauncher"
    skipCopyingDependencies = true

    // jfxNative
    identifier = "BilledTrain380"
    nativeReleaseVersion = "$sporttagPSAVersion"
    vendor = "BilledTrain380"
    bundleArguments
    appName = "PSA"
    skipMainClassScanning = true

    // TODO: add jvm arguments to optimize runtime
}

/*
 * This task must never be executed. In order to build the application,
 * the bootJar task is used.
 */
jfxJar {
    onlyIf {
        false
    }
    doLast {
        println("Skip jfxJar. bootJar is used to build this application.")
    }
}

// Copies the jar file generated from bootJar task to the jfx app output directory.
task jfxPrepare(type: Copy) {
    dependsOn("bootJar")
    from "build/libs"
    into "build/jfx/app"
}

jfxNative.dependsOn("jfxPrepare")
