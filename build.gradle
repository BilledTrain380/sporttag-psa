buildscript {
	ext {
		kotlin_version = '1.2.30'
		springBootVersion = '1.5.3.RELEASE'
		sporttagPSAVersion = '1.0.0-beta-1'
	}
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		mavenCentral()
        
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlin_version}")
		classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
        classpath "org.jetbrains.kotlin:kotlin-noarg:${kotlin_version}"
        classpath("com.github.cr0:gradle-macappbundle-plugin:3.1.0")
	}
}

apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: "kotlin-jpa"
apply plugin: "com.github.cr0.macappbundle"

version = "${sporttagPSAVersion}"

allprojects {

	// TODO: Remove kotlin stuff
	
	apply plugin: 'idea'
	apply plugin: 'kotlin'

	sourceCompatibility = 1.8
	compileKotlin {
		kotlinOptions.jvmTarget= "1.8"
		targetCompatibility = 1.8
	}

	compileTestKotlin {
		kotlinOptions.jvmTarget = "1.8"
		targetCompatibility = 1.8
	}

	repositories {
		maven { url "http://dl.bintray.com/jetbrains/spek" }
		maven { url "https://dl.bintray.com/kotlin/kotlin-dev/"}
		mavenCentral()
	}
	
	dependencies {
		compile("org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}")
	}
}

project(":filesystem") {
    
    apply plugin: 'kotlin-spring'
    
	dependencies {
		compile group: 'org.springframework.boot', name: 'spring-boot', version: '1.5.3.RELEASE'
	}
}

project(":macos") {

	apply plugin: 'kotlin-spring'

	dependencies {
		compile group: 'org.springframework.boot', name: 'spring-boot', version: '1.5.3.RELEASE'
	}
}

repositories {
	maven { url 'https://jitpack.io' }
	maven {
		url "https://jaspersoft.artifactoryonline.com/jaspersoft/jr-ce-releases/"
	}
}

macAppBundle {
    appName = "Sporttag PSA"
    appCategory = "public.app-category.utilities"
    icon = "macos/icon/psa-logo.icns"
    agent = true
    version = "${sporttagPSAVersion}"
    shortVersion = "${sporttagPSAVersion}"
    // DO NOT USE THE MAIN CLASS OF YOUR APPLICATION!!!
    // With gradle task build, the bootRepackage goal will be executed.
    // If this is the case, the JarLauncher class of spring is needed as main class.
    // Otherwise your main class will not be found, because the bootRepackage goal will
    // change your directory structure and MANIFEST.inf
    mainClassName = "org.springframework.boot.loader.JarLauncher"
    bundleJRE = true
    jreHome = System.getenv("JAVA_HOME")
    bundleExecutable = "Sporttag-psa"
    bundleIdentifier = "ch.schulealtendorf.sporttagpsa"
    bundleCopyright = "Copyright (C) 2018 Sporttag PSA"
    volumeName = "Sporttag PSA ${sporttagPSAVersion}"
    archiveName = "Sporttag PSA ${sporttagPSAVersion}"
    javaXProperties=[
            "ms25M",
            "mx80M",
            "X:+UseG1GC"
    ]
}

createApp.dependsOn(bootRepackage)

junitPlatform {
	filters {
		engines {
			include 'spek'
		}
	}
}

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
	
	compile('org.springframework.boot:spring-boot-starter-thymeleaf')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-security:1.5.3.RELEASE')
	compile("org.springframework.boot:spring-boot-devtools")
    
    compile "com.opencsv:opencsv:3.9"
	compile "joda-time:joda-time:2.9.9"
	compile "com.deliveredtechnologies:rulebook-core:0.8"
	compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.9.4.1'
	
	compile project(":rules")
    compile project(":filesystem")
    compile project(":macos")

	compile 'com.github.BilledTrain380:Alberta:develop-SNAPSHOT'
	
    // database dependencies
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.flywaydb:flyway-core')
	runtime('com.h2database:h2')
	
	// to enable thymeleaf legacy html5 mode
	compile group: 'net.sourceforge.nekohtml', name: 'nekohtml', version: '1.9.22'
	
	// webjars
	compile 'org.webjars:jquery:3.2.0'
	compile 'org.webjars:bootstrap:4.0.0-alpha.6'
	compile 'org.webjars:font-awesome:4.7.0'
	compile 'org.webjars:tether:1.4.0'
	
    // basic testing libraries
	testCompile('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.mockito'
    }
	testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.9.0'
    testCompile ('com.nhaarman:mockito-kotlin:1.5.0') {
        exclude group: 'org.mockito'
    }
	testCompile "org.jetbrains.kotlin:kotlin-test:1.1.4-dev-330"
    
    // A specification framework
    testCompile "org.jetbrains.spek:spek-api:1.1.2"
	
	// necessary for spek on JUnit4
	testCompile "org.jetbrains.spek:spek-junit-platform-engine:1.1.2"
	testCompile "org.junit.platform:junit-platform-runner:1.0.0-M4"
	testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.2'
}

// copy resources to see resource changes
task copyResources(type: Copy) {
	from 'src/main/resources/'
	into 'build/resources/main/'
}